import math/geometry;
import xianxia_clan_war/game_types;
import ds/arrayutils;
import xianxia_clan_war/state/world_state;

export {
	collide(obj : GameObject, obj2 : GameObject, dist : double) -> bool;
	moveTowards(moved : GameObject, towards : GameObject, world : WorldState) -> GameObject;
	collidesWith(obj : GameObject, world : WorldState, dist : double) -> [GameObject];
}

gameSpeedup = 5.0;

collide(obj : GameObject, obj2 : GameObject, dist : double) -> bool {
	bounds = getValue(obj.bounds);
	bounds2 = getValue(obj2.bounds);

	obj != obj2 &&
	distanceP(bounds.coords, bounds2.coords) < bounds.radius + bounds2.radius + dist &&
	intersection(obj.schema.layers, obj2.schema.layers) != []
}

collidesWith(obj : GameObject, world : WorldState, dist : double) -> [GameObject] {
	objectsFromSamePlane = getNeighbourObjects(obj, world);
	filter(objectsFromSamePlane, \o -> collide(obj, o, dist));
}

moveTowards(moved : GameObject, towards : GameObject, world : WorldState) -> GameObject {
	b = getValue(moved.bounds);
	direction = normP(minusP(getValue(towards.bounds).coords, b.coords));
	avoid = avoidCollision(moved, world);
	movement = multP(if (lengthP(avoid) > 0.01) avoid else direction, moved.schema.speed * gameSpeedup);
	next(moved.bounds, BCircle(b.radius, plusP(b.coords, movement)));
	moved
}

normP(a : Point) -> Point {
	multP(a, 1.0 / max(lengthP(a), 0.00001))
}

square(val : double) -> double {
	val * val
}

distanceP(a : Point, b : Point) -> double {
	sqrt(square(a.x - b.x) + square(a.y - b.y))
}

lengthP(a : Point) -> double {
	distanceP(vecInit, a)
}

vecInit = Point(0.0, 0.0);

multP(a : Point, n : double) -> Point {
	Point(a.x * n, a.y * n)
}

minusP(a : Point, b : Point) -> Point {
	Point(a.x - b.x, a.y - b.y)
}

plusP(a : Point, b : Point) -> Point {
	Point(a.x + b.x, a.y + b.y)
}

ortPoint(a : Point) -> Point {
	Point(a.y, -a.x)
}

avoidCollision(moved : GameObject, world : WorldState) -> Point {
	colliding = collidesWith(moved, world, 0.0);
	bounds = getValue(moved.bounds);
	collisionVec = fold(colliding, vecInit, \vec, obj -> {
		collBounds = getValue(obj.bounds);
		direction = normP(minusP(collBounds.coords, bounds.coords));
		plusP(vec, multP(direction, distanceP(collBounds.coords, bounds.coords)))
	});
	multP(ortPoint(normP(collisionVec)), 1.0 / i2d(max(length(colliding), 1)));
}

// test() {
// 	w1 = WorldState(Point(0.0, 0.0), [mkGameObject(HARE, Point(1.0, 1.0)), mkGameObject(HARE, Point(2.0, 2.0))], []);
// 	println(avoidCollision(w1.gameObjects[0], w1));
// 	println(avoidCollision(w1.gameObjects[1], w1));

// 	w2 = WorldState(Point(0.0, 0.0), [mkGameObject(HARE, Point(1.0, 1.0)), mkGameObject(HARE, Point(1.0, 1.0))], []);
// 	println(avoidCollision(mkGameObject(HARE, Point(1.0, 1.0)), w2));
// }